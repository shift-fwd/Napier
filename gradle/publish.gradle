import dependencies.Versions

apply plugin: 'maven-publish'

group = BINTRAY_PACKAGE
version = Versions.versionName

publishing.publications.all {
    pom {
        description = POM_DESCRIPTION
        url = SITE_URL
        licenses {
            license {
                name = POM_LICENSE_NAME
                url = POM_LICENSE_URL
                distribution = POM_LICENSE_DIST
            }
        }
        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
                organization = POM_ORGANIZATION_NAME
                organizationUrl = POM_ORGANIZATION_URL
            }
        }
        scm {
            url = SITE_URL
        }
    }
}
def nexusUser = hasProperty('nexusUsername') ? nexusUsername : System.getenv('NEXUS_CREDS_USR')
def nexusPassword = hasProperty('nexusPassword') ? nexusPassword : System.getenv('NEXUS_CREDS_PSW')
publishing {
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
                url "https://nexus.shiftinsurtech.com/repository/maven-snapshots"
            } else {
                url "https://nexus.shiftinsurtech.com/repository/maven-releases"
            }

            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        mavenLocal()
    }
}
